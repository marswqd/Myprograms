******
* 此为SAC宏，功能是对两个时间序列文件进行互相关，
* 为双台群速度频散提取中的互相关方法的多重滤波步骤提供SAC文件。
* 自动读入两个SAC文件以及最终生成的文件名
* 互相关计算――――修改文件头
******
******
* 互相关计算
******
$keys in1 in2 out
r $in1 $in2
evaluate to dist1 &1,DIST
evaluate to dist2 &2,DIST
correlate
w cor11 cor12

******
* 改变互相关文件头
******
r cor12
*lh
evaluate to dist %dist1 - %dist2

if %dist LT 0.0
 evaluate to dist %dist * -1.0
endif

ch DIST %dist
w $out

******
* 计算相关系数
******
r $in1 $in2
evaluate to m1 &1,DEPMEN
evaluate to m2 &2,DEPMEN
sqr
w s1 s2
r s1 s2
evaluate to mm1 &1,DEPMEN
evaluate to mm2 &2,DEPMEN
*lh
r $in1
mulf $in2
w s3
r s3
*lh
evaluate to m12 &1,DEPMEN
getbb m1 m2 mm1 mm2 m12
evaluate to up %m12 - ( %m1 * %m2 )
getbb up
evaluate to down1 %mm1 - ( %m1 ** 2 )
getbb down1
evaluate to down2 %mm2 - ( %m2 ** 2 )
getbb down2
evaluate to down3 %down1 * %down2
getbb down3
evaluate to down sqrt %down3
getbb down
evaluate to r12 %up / %down

setbb m '$in1$+$in2$=$out$'
getbb to correcoeff names off newline off m r12

unsetbb all
sc rm -f s1 s2 s3 cor11 cor12

q